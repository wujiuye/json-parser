plugins {
    id 'java'
    // maven 发布插件
    id 'maven-publish'
    // 签名插件
    id 'signing'
}

group = 'com.github.wujiuye'
version = '2.0.3.RELEASE'

dependencies {
    compile("${group}:json-parser-core:${version}")

    compileOnly('org.springframework:spring-context:5.2.6.RELEASE')
    compileOnly('org.springframework:spring-web:5.2.6.RELEASE')
    compileOnly('org.springframework:spring-webmvc:5.2.6.RELEASE')
    compileOnly('org.springframework:spring-webflux:5.2.6.RELEASE')
    compileOnly('com.fasterxml.jackson.core:jackson-databind:2.9.6')

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// 打包源码
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

// 生成 javadoc jar
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        // 推送
        publishMyProject(MavenPublication) {
            // 编译jar包
            from components.java
            // 打包源码
            artifact sourcesJar
            // javadoc
            artifact javadocJar

            pom {
                name = "json-parser-adapter"
                description = "json-parser-adapter是json-parser适配springboot的starter包，自动将webmvc或webflux的json解析工作交给json-parser完成，以此让整个项目使用同一套json解析配置。"
                url = "https://github.com/wujiuye/json-parser"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "wujiuye"
                        name = "wujiuye"
                        email = "419611821@qq.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/wujiuye/json-parser.git"
                    developerConnection = "scm:git:https://github.com/wujiuye/json-parser.git"
                    url = "https://github.com/wujiuye/json-parser"
                }
            }
        }
    }
    // 到 https://oss.sonatype.org/#nexus-search; 搜索是否上传成功
    repositories {
        // 发布Release版本可在版本号后面带上'-RELEASE'，也可不带
        maven {
            name 'Release'
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username = "${NEXUS_USERNAME}"
                password = "${NEXUS_PASSWORD}"
            }
        }
        // 发布Snapshot快照版本必须在版本号后面加上'-SNAPSHOT'
        maven {
            name = 'Snapshot'
            url = 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials {
                username = "${NEXUS_USERNAME}"
                password = "${NEXUS_PASSWORD}"
            }
        }
    }
}

// 必须在 publishing 配置之后
signing {
    sign publishing.publications.publishMyProject
}

